#pragma once

#include "../../arch/common.cuh"
#include "../../arch/mma_sm120.cuh"
#include "../../arch/ldmatrix_sm120.cuh"
#include "../../arch/barrier.cuh"
#include "../../arch/cp_async.cuh"
#include "../../model/kv_cache_traits.cuh"
#include "../../model/scale_convert.cuh"
#include "../common/smem_layout.cuh"
#include "../common/kv_cache_io.cuh"
#include "../common/fp8_quant.cuh"
#include "../common/online_softmax.cuh"
#include "../common/q_rope.cuh"
#include "../common/d2_load_b.cuh"
#include "../common/xv_rope_mma.cuh"

// ============================================================================
// Sparse MLA Prefill Kernel — single-pass (no split-KV, no combine)
//
// Structurally identical to decode main loop (QK→softmax→XV), but:
//   - Iterates over ALL NI = TOPK/BI tiles (no split)
//   - Writes direct BF16 output (no partial_O + combine)
//   - No PDL (no dependent kernel)
//
// Template params (all constexpr):
//   MT:              ModelType (V32 / MODEL1)
//   CM:              ComputeMode (FP8 / BF16) — currently FP8 only
//   NUM_HEADS:       16, 64, 128
//   TOPK:            512, 1024, 2048
//   PAGE_BLOCK_SIZE: 1 (V32) or 64 (MODEL1)
// ============================================================================

struct PrefillColdParams {
    float sm_scale;
    int num_tokens;
    size_t stride_kv_block;
    // Used only by the MG kernel when TOPK_EXTRA > 0 (dual-cache); the SG
    // kernel and single-cache MG path ignore it.
    size_t stride_kv_block_extra;
    const float* attn_sink;  // [NUM_HEADS] float32, natural log domain. nullptr = disabled.
    const int* topk_length;  // [num_tokens] int32, nullptr = uniform TOPK.
    const int* topk_length_extra;  // [num_tokens] int32, dual-cache only. nullptr = uniform TOPK_EXTRA.
};

template <ModelType MT, ComputeMode CM, int NUM_HEADS, int TOPK, int PAGE_BLOCK_SIZE>
__global__ void __launch_bounds__(BLOCK_THREADS, 1)
sparse_mla_prefill_kernel(
    const bf16* __restrict__ Q,
    const uint8_t* __restrict__ KV_cache,
    const int32_t* __restrict__ indices,
    const float* __restrict__ attn_sink,  // [NUM_HEADS], nullable
    bf16* __restrict__ output,
    float* __restrict__ out_lse,
    __grid_constant__ const PrefillColdParams cold)
{
    const float sm_scale = cold.sm_scale;
    const int num_tokens = cold.num_tokens;
    constexpr int page_block_size = PAGE_BLOCK_SIZE;
    const size_t stride_kv_block = cold.stride_kv_block;
    using KV = KVCacheTraits<MT>;
    using CT = ComputeTraits<MT, CM>;
    using L = SmemLayout<MT, CM>;
    using IO = KVIOTraits<MT>;

    static constexpr int NI = TOPK / BI;
    static constexpr int REPLICATE_H = NUM_HEADS / HPB;
    static constexpr int QK_NOPE_KSTEPS = KV::QUANT_TILE / 32;

    const int s_i = blockIdx.x / REPLICATE_H;
    const int h_tile = blockIdx.x % REPLICATE_H;
    const int h_start = h_tile * HPB;
    if (s_i >= num_tokens) return;

    const int topk_len = cold.topk_length ? __ldg(cold.topk_length + s_i) : TOPK;
    const int actual_ni = (topk_len + BI - 1) / BI;

    const int warp_rank = threadIdx.x / 32;
    const int wy = warp_rank / 4;

    extern __shared__ char smem_raw[];
    auto sm = SmemPtrs<MT, CM>::init(smem_raw);

    if (threadIdx.x == 0) {
        mbarrier_init(sm.mbar_kv + 0, 1);
        mbarrier_init(sm.mbar_kv + 1, 1);
    }
    bar_sync_t<3, BLOCK_THREADS>();

    // ── IO warps ────────────────────────────────────────────────────
    if (wy == 2) {
        asm volatile("setmaxnreg.dec.sync.aligned.u32 %0;\n" :: "n"(32));

        const int io_tid = threadIdx.x - N_MATH_WARPS * 32;
        const int32_t* idx_base = indices + (size_t)s_i * TOPK;
        const uint64_t kv_l2_policy = create_l2_evict_first_policy();

        // Prologue: gather tile 0
        io_bulk_gather_tile<MT, PAGE_BLOCK_SIZE, true>(
            sm.kv_bufs[0], idx_base, KV_cache, sm.mbar_kv + 0, io_tid,
            stride_kv_block, kv_l2_policy);
        io_gather_scales<MT, PAGE_BLOCK_SIZE>(
            sm.kv_scale_bufs[0], idx_base, KV_cache, io_tid,
            stride_kv_block);
        __threadfence_block();

        #pragma unroll 1
        for (int ti = 0; ti < actual_ni; ti++) {
            if (ti + 1 < actual_ni) {
                io_bulk_gather_tile<MT, PAGE_BLOCK_SIZE, true>(
                    sm.kv_bufs[(ti + 1) & 1],
                    idx_base + (ti + 1) * BI, KV_cache,
                    sm.mbar_kv + ((ti + 1) & 1), io_tid,
                    stride_kv_block, kv_l2_policy);
                io_gather_scales<MT, PAGE_BLOCK_SIZE>(
                    sm.kv_scale_bufs[(ti + 1) & 1],
                    idx_base + (ti + 1) * BI, KV_cache, io_tid,
                    stride_kv_block);
                __threadfence_block();
            }
            bar_sync_t<1, BLOCK_THREADS>();
        }

    // ── Math warps ──────────────────────────────────────────────────
    } else {
        asm volatile("setmaxnreg.inc.sync.aligned.u32 %0;\n" :: "n"(232));

        const int lane = threadIdx.x & 31;
        const int mwarp = warp_rank;
        const int gid = lane >> 2, tid = lane & 3;
        const float sm_scale_log2e = sm_scale * LOG2E;
        const bf16* q_base = Q + (size_t)s_i * NUM_HEADS * KV::D_QK + (size_t)h_start * KV::D_QK;
        const int32_t* idx_base = indices + (size_t)s_i * TOPK;

        quantize_q_to_smem<MT, MATH_THREADS>(
            sm.q_nope_fp8, sm.q_nope_sc, sm.q_rope, q_base, sm.reduce_buf);
        QRopeRegs q_rope_regs = preload_q_rope_regs(sm.q_rope, lane);

        for (int h = threadIdx.x; h < HPB; h += MATH_THREADS)
            sm.m_smem[h] = -1e30f;

        float acc_o[CT::ACC_TILES][4];
        #pragma unroll
        for (int t = 0; t < CT::ACC_TILES; t++)
            acc_o[t][0] = acc_o[t][1] = acc_o[t][2] = acc_o[t][3] = 0.f;

        float acc_rope[4] = {0.f, 0.f, 0.f, 0.f};
        float warp_l[2] = {0.f, 0.f};

        bar_sync_t<2, MATH_THREADS>();
        mbarrier_wait_parity(sm.mbar_kv + 0, 0);

        // ── Main loop — QK + softmax + XV ───────────────────────────
        #pragma unroll 1
        for (int ti = 0; ti < actual_ni; ti++) {
            uint8_t* kv_smem = sm.kv_bufs[ti & 1];
            const int32_t* ib = idx_base + ti * BI;
            const int qk_nb = mwarp * ENTRIES_PER_WARP;
            uint8_t* kv_warp_base = kv_smem + qk_nb * KV::KV_SMEM_STRIDE;

            const uint8_t* entry_base[ENTRIES_PER_WARP];
            if constexpr (KV::V_HAS_ROPE) {
                #pragma unroll
                for (int e = 0; e < ENTRIES_PER_WARP; e++) {
                    int idx = ib[qk_nb + e];
                    idx = (idx >= 0) ? idx : 0;
                    int bi_e = idx / page_block_size;
                    int li_e = idx % page_block_size;
                    entry_base[e] = KV_cache + (size_t)bi_e * stride_kv_block
                                             + (size_t)li_e * IO::IO_STRIDE;
                }
            } else {
                int idx = ib[qk_nb + gid];
                idx = (idx >= 0) ? idx : 0;
                entry_base[gid] = KV_cache + (size_t)idx * IO::IO_STRIDE;
            }

            for (int i = threadIdx.x; i < CT::N_V_CHUNKS * HPB; i += MATH_THREADS)
                sm.w_head_sc_all[i] = 0.f;

            KVRopePrefetch rope_pf = prefetch_kv_rope(
                reinterpret_cast<const bf16*>(entry_base[gid] + KV::KV_ROPE_GMEM_OFFSET), lane);

            // ── QK nope (block-scaled FP8 MMA) ─────────────────────
            float qk[4] = {0.f, 0.f, 0.f, 0.f};
            const uint8_t* kv_gid_base = kv_warp_base + gid * KV::KV_SMEM_STRIDE;
            #pragma unroll
            for (int blk = 0; blk < KV::NUM_SCALES; blk++) {
                uint8_t sfa = fp32_to_ue8m0(
                    sm.q_nope_sc[(gid + (lane & 1) * 8) * KV::NUM_SCALES + blk]);
                uint8_t sfb;
                if constexpr (KV::SCALE_IN_KV_SMEM) {
                    sfb = fp32_to_ue8m0(
                        reinterpret_cast<const float*>(kv_gid_base + KV::D_NOPE)[blk]);
                } else {
                    sfb = sm.kv_scale_bufs[ti & 1][(qk_nb + gid) * KV::SCALE_BYTES_PER_TOKEN + blk];
                }

                #pragma unroll
                for (int ks = 0; ks < QK_NOPE_KSTEPS; ks++) {
                    int ko = blk * KV::QUANT_TILE + ks * 32;
                    uint32_t a0, a1, a2, a3, b0, b1;
                    ldmatrix_load_A_fp8(a0, a1, a2, a3,
                        sm.q_nope_fp8 + ko, KV::Q_NOPE_STRIDE, lane);
                    ldmatrix_load_B_fp8(b0, b1,
                        kv_warp_base + ko, KV::KV_SMEM_STRIDE, lane);
                    MmaFp8Result r = mma_fp8_block_scaled_m16n8k32(
                        a0, a1, a2, a3, b0, b1,
                        qk[0], qk[1], qk[2], qk[3], sfa, sfb);
                    qk[0] = r.d0; qk[1] = r.d1; qk[2] = r.d2; qk[3] = r.d3;
                }
            }

            // ── QK rope (BF16 MMA, uses prefetched B operands) ──────
            compute_qk_rope(qk, q_rope_regs, rope_pf);

            // ── Invalid index masking + topk_length overflow ─────
            {
                int e0 = qk_nb + tid * 2, e1 = e0 + 1;
                if (ib[e0] < 0) { qk[0] = -1e30f; qk[2] = -1e30f; }
                if (ib[e1] < 0) { qk[1] = -1e30f; qk[3] = -1e30f; }
                if (cold.topk_length != nullptr) {
                    int a0 = ti * BI + e0, a1 = ti * BI + e1;
                    if (a0 >= topk_len) { qk[0] = -1e30f; qk[2] = -1e30f; }
                    if (a1 >= topk_len) { qk[1] = -1e30f; qk[3] = -1e30f; }
                }
            }

            // ── Online softmax (deferred sum, conditional rescale) ──
            float s[4] = { qk[0] * sm_scale_log2e, qk[1] * sm_scale_log2e,
                           qk[2] * sm_scale_log2e, qk[3] * sm_scale_log2e };

            float lm0, lm1;
            softmax_warp_max(s, lm0, lm1);
            if (tid == 0) {
                sm.reduce_buf[mwarp * HPB + gid] = lm0;
                sm.reduce_buf[mwarp * HPB + gid + 8] = lm1;
            }
            bar_sync_t<2, MATH_THREADS>();

            if (threadIdx.x < HPB) {
                int h = threadIdx.x;
                float old_m = sm.m_smem[h], tm = -1e30f;
                #pragma unroll
                for (int w = 0; w < N_MATH_WARPS; w++)
                    tm = fmaxf(tm, sm.reduce_buf[w * HPB + h]);
                float nm = fmaxf(old_m, tm);
                float alpha = exp2f(old_m - nm);
                sm.m_smem[h] = nm;
                sm.reduce_buf[h] = alpha;
                sm.reduce_buf[HPB + h] = nm;
            }
            bar_sync_t<2, MATH_THREADS>();

            float alpha0 = sm.reduce_buf[gid], alpha1 = sm.reduce_buf[gid + 8];
            float nm0 = sm.reduce_buf[HPB + gid], nm1 = sm.reduce_buf[HPB + gid + 8];

            if (alpha0 < 1.0f || alpha1 < 1.0f) {
                #pragma unroll
                for (int t = 0; t < CT::ACC_TILES; t++) {
                    acc_o[t][0] *= alpha0; acc_o[t][1] *= alpha0;
                    acc_o[t][2] *= alpha1; acc_o[t][3] *= alpha1;
                }
                if constexpr (KV::V_HAS_ROPE) {
                    acc_rope[0] *= alpha0; acc_rope[1] *= alpha0;
                    acc_rope[2] *= alpha1; acc_rope[3] *= alpha1;
                }
                warp_l[0] *= alpha0;
                warp_l[1] *= alpha1;
            }

            float w0 = exp2f(s[0] - nm0), w1 = exp2f(s[1] - nm0);
            float w2 = exp2f(s[2] - nm1), w3 = exp2f(s[3] - nm1);

            float ls0, ls1;
            softmax_warp_sum(w0, w1, w2, w3, ls0, ls1);
            warp_l[0] += ls0;
            warp_l[1] += ls1;

            // ── V scale cache + atomicMax ───────────────────────────
            float vsc_cache[CT::N_V_CHUNKS][2];
            {
                const int e0i = qk_nb + tid * 2, e1i = e0i + 1;
                const uint8_t* e0_base = kv_warp_base + tid * 2 * KV::KV_SMEM_STRIDE;
                const uint8_t* e1_base = e0_base + KV::KV_SMEM_STRIDE;
                #pragma unroll
                for (int vc = 0; vc < CT::N_V_CHUNKS; vc++) {
                    if constexpr (KV::SCALE_IN_KV_SMEM) {
                        vsc_cache[vc][0] = reinterpret_cast<const float*>(e0_base + KV::D_NOPE)[vc];
                        vsc_cache[vc][1] = reinterpret_cast<const float*>(e1_base + KV::D_NOPE)[vc];
                    } else {
                        vsc_cache[vc][0] = ue8m0_to_fp32(sm.kv_scale_bufs[ti & 1][e0i * KV::SCALE_BYTES_PER_TOKEN + vc]);
                        vsc_cache[vc][1] = ue8m0_to_fp32(sm.kv_scale_bufs[ti & 1][e1i * KV::SCALE_BYTES_PER_TOKEN + vc]);
                    }
                    float ws00 = w0 * vsc_cache[vc][0], ws01 = w1 * vsc_cache[vc][1];
                    float ws10 = w2 * vsc_cache[vc][0], ws11 = w3 * vsc_cache[vc][1];
                    atomicMax(reinterpret_cast<int*>(&sm.w_head_sc_all[vc * HPB + gid]),
                        __float_as_int(fmaxf(fabsf(ws00), fabsf(ws01))));
                    atomicMax(reinterpret_cast<int*>(&sm.w_head_sc_all[vc * HPB + gid + 8]),
                        __float_as_int(fmaxf(fabsf(ws10), fabsf(ws11))));
                }
            }
            bar_sync_t<2, MATH_THREADS>();

            for (int i = threadIdx.x; i < CT::N_V_CHUNKS * HPB; i += MATH_THREADS)
                sm.w_head_sc_all[i] = fmaxf(sm.w_head_sc_all[i], 1e-10f) / FP8_MAX;
            bar_sync_t<2, MATH_THREADS>();

            // ── XV nope MMA (D2: direct B from kv_smem) ───────────
            {
                const int e0i = qk_nb + tid * 2, e1i = e0i + 1;

                // Batch W quant for all chunks
                #pragma unroll
                for (int vc = 0; vc < CT::N_V_CHUNKS; vc++) {
                    float* vc_sc = sm.w_head_sc_all + vc * HPB;
                    uint8_t* wfp8 = sm.w_fp8 + vc * L::SMEM_W_FP8_ONE;
                    float si0 = 1.f / vc_sc[gid], si1 = 1.f / vc_sc[gid + 8];
                    float vsc0 = vsc_cache[vc][0], vsc1 = vsc_cache[vc][1];
                    float ws00 = w0 * vsc0, ws01 = w1 * vsc1;
                    float ws10 = w2 * vsc0, ws11 = w3 * vsc1;
                    __nv_fp8_e4m3 f00(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws00 * si0)));
                    __nv_fp8_e4m3 f01(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws01 * si0)));
                    __nv_fp8_e4m3 f10(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws10 * si1)));
                    __nv_fp8_e4m3 f11(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws11 * si1)));
                    wfp8[gid * (BI + 16) + e0i] = f00.__x;
                    wfp8[gid * (BI + 16) + e1i] = f01.__x;
                    wfp8[(gid + 8) * (BI + 16) + e0i] = f10.__x;
                    wfp8[(gid + 8) * (BI + 16) + e1i] = f11.__x;
                }
                bar_sync_t<2, MATH_THREADS>();

                // Batch MMA for all chunks
                #pragma unroll
                for (int vc = 0; vc < CT::N_V_CHUNKS; vc++) {
                    float* vc_sc = sm.w_head_sc_all + vc * HPB;
                    uint8_t* wfp8 = sm.w_fp8 + vc * L::SMEM_W_FP8_ONE;
                    #pragma unroll
                    for (int nt = 0; nt < CT::NT_PER_WARP_XV; nt++) {
                        int ti_acc = vc * CT::NT_PER_WARP_XV + nt;
                        int dim = vc * CT::V_CHUNK + mwarp * (CT::NT_PER_WARP_XV * 8) + nt * 8;
                        float xv[4] = {0.f, 0.f, 0.f, 0.f};
                        #pragma unroll
                        for (int kstep = 0; kstep < CT::XV_KSTEPS; kstep++) {
                            int ko = kstep * 32;
                            uint32_t a0, a1, a2, a3, b0, b1;
                            ldmatrix_load_A_fp8(a0, a1, a2, a3,
                                wfp8 + ko, BI + 16, lane);
                            d2_load_b_fp8<KV::KV_SMEM_STRIDE>(b0, b1,
                                kv_smem, kstep * 32, dim, lane);
                            MmaFp8Result r = mma_fp8_m16n8k32(
                                a0, a1, a2, a3, b0, b1,
                                xv[0], xv[1], xv[2], xv[3]);
                            xv[0] = r.d0; xv[1] = r.d1; xv[2] = r.d2; xv[3] = r.d3;
                        }
                        float sc0 = vc_sc[gid], sc1 = vc_sc[gid + 8];
                        acc_o[ti_acc][0] += xv[0] * sc0; acc_o[ti_acc][1] += xv[1] * sc0;
                        acc_o[ti_acc][2] += xv[2] * sc1; acc_o[ti_acc][3] += xv[3] * sc1;
                    }
                }
            }

            if constexpr (!KV::V_HAS_ROPE)
                bar_sync_t<2, MATH_THREADS>();

            // ── XV rope BF16 MMA (MODEL1 only) ─────────────────────
            if constexpr (KV::V_HAS_ROPE) {
                bar_sync_t<2, MATH_THREADS>();
                xv_rope_mma<MT, PAGE_BLOCK_SIZE>(acc_rope, w0, w1, w2, w3,
                    ib, KV_cache, mwarp, lane,
                    stride_kv_block,
                    reinterpret_cast<bf16*>(sm.w_fp8));
            }

            bar_arrive_t<1, BLOCK_THREADS>();
            if (ti + 1 < actual_ni) {
                const int next_phase = ((ti + 1) >> 1) & 1;
                mbarrier_wait_parity(sm.mbar_kv + ((ti + 1) & 1), next_phase);
            }
        }

        // ── Finalize deferred row_sum ────────────────────────────────
        if (tid == 0) {
            sm.reduce_buf[mwarp * HPB + gid] = warp_l[0];
            sm.reduce_buf[mwarp * HPB + gid + 8] = warp_l[1];
        }
        bar_sync_t<2, MATH_THREADS>();
        if (threadIdx.x < HPB) {
            int h = threadIdx.x;
            float ts = 0.f;
            #pragma unroll
            for (int w = 0; w < N_MATH_WARPS; w++)
                ts += sm.reduce_buf[w * HPB + h];
            sm.l_smem[h] = ts;
        }
        bar_sync_t<2, MATH_THREADS>();

        // ── Write BF16 output and LSE ────────────────────────────────
        // attn_sink convention (FlashMLA V4): output[h] *= sigmoid(lse_h - sink_h)
        // is folded directly into the normalizer:
        //   il = exp(lse) / (exp(lse) + exp(sink)) / exp(lse)
        //      = 1 / (l + exp(sink - m))   in log2 space
        // (working in log2 space: sum_l is in exp-domain of m, multiply sink by LOG2E).
        // Padded heads carry sink=-inf → exp2(-inf)=0 → no-op (collapses to 1/l).
        float il0, il1;
        if (cold.attn_sink != nullptr) {
            float s0 = __ldg(cold.attn_sink + h_start + gid) * LOG2E;
            float s1 = __ldg(cold.attn_sink + h_start + gid + 8) * LOG2E;
            float denom0 = sm.l_smem[gid] + exp2f(s0 - sm.m_smem[gid]);
            float denom1 = sm.l_smem[gid + 8] + exp2f(s1 - sm.m_smem[gid + 8]);
            il0 = (denom0 > 0.f) ? (1.f / denom0) : 0.f;
            il1 = (denom1 > 0.f) ? (1.f / denom1) : 0.f;
        } else {
            il0 = (sm.l_smem[gid] > 0.f) ? (1.f / sm.l_smem[gid]) : 0.f;
            il1 = (sm.l_smem[gid + 8] > 0.f) ? (1.f / sm.l_smem[gid + 8]) : 0.f;
        }

        bf16* staging_bf16 = reinterpret_cast<bf16*>(sm.kv_bufs[0]);
        constexpr int BF16_STAGING_STRIDE = D_V;

        #pragma unroll
        for (int t = 0; t < CT::ACC_TILES; t++) {
            constexpr int _NT8 = CT::NT_PER_WARP_XV * 8;
            int c = t / CT::NT_PER_WARP_XV, lnt = t % CT::NT_PER_WARP_XV;
            int d0 = c * CT::V_CHUNK + mwarp * _NT8 + lnt * 8 + tid * 2;
            staging_bf16[gid * BF16_STAGING_STRIDE + d0]       = __float2bfloat16(acc_o[t][0] * il0);
            staging_bf16[gid * BF16_STAGING_STRIDE + d0 + 1]   = __float2bfloat16(acc_o[t][1] * il0);
            staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0]   = __float2bfloat16(acc_o[t][2] * il1);
            staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0+1] = __float2bfloat16(acc_o[t][3] * il1);
        }

        if constexpr (KV::V_HAS_ROPE) {
            int n_start = mwarp * 8;
            int d0 = KV::D_NOPE + n_start + tid * 2;
            staging_bf16[gid * BF16_STAGING_STRIDE + d0]       = __float2bfloat16(acc_rope[0] * il0);
            staging_bf16[gid * BF16_STAGING_STRIDE + d0 + 1]   = __float2bfloat16(acc_rope[1] * il0);
            staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0]   = __float2bfloat16(acc_rope[2] * il1);
            staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0+1] = __float2bfloat16(acc_rope[3] * il1);
        }
        bar_sync_t<2, MATH_THREADS>();

        // Coalesced BF16 write: uint4 = 128-bit = 8 bf16 per store
        {
            constexpr size_t h_stride = D_V;
            constexpr size_t token_stride = (size_t)NUM_HEADS * D_V;
            const size_t out_base = (size_t)s_i * token_stride
                                  + (size_t)h_start * h_stride;
            constexpr int BF16_PER_STORE = 8;
            constexpr int STORES_PER_HEAD = D_V / BF16_PER_STORE;  // 64
            for (int idx = threadIdx.x; idx < HPB * STORES_PER_HEAD; idx += MATH_THREADS) {
                int h = idx / STORES_PER_HEAD;
                int d8 = (idx - h * STORES_PER_HEAD) * BF16_PER_STORE;
                uint4 v = *reinterpret_cast<const uint4*>(
                    &staging_bf16[h * BF16_STAGING_STRIDE + d8]);
                *reinterpret_cast<uint4*>(&output[out_base + h * h_stride + d8]) = v;
            }
        }

        // Write LSE (merged with attn_sink if present)
        if (threadIdx.x < HPB) {
            int h = threadIdx.x;
            float lse = softmax_lse(sm.m_smem[h], sm.l_smem[h]);
            if (cold.attn_sink != nullptr) {
                float sink_log2 = __ldg(cold.attn_sink + h_start + h) * LOG2E;
                if (lse != -1e30f)
                    lse += log2f(1.f + exp2f(sink_log2 - lse));
                else
                    lse = sink_log2;
            }
            size_t lse_idx = (size_t)s_i * NUM_HEADS + h_start + h;
            out_lse[lse_idx] = lse;
        }
    }
}

// ============================================================================
// Multi-Group (MG) Prefill Kernel — 2 head groups per CTA
//
// Processes 2×HPB = 32 heads per CTA. KV loaded once, reused for both groups.
// Key optimizations over SG:
//   - 2x KV reuse (V transpose shared, smem KV shared)
//   - Deferred row_sum (warp_l_partial in registers, reduce once at end)
//   - Better compute/load ratio → higher MMA utilization
//
// Used for NUM_HEADS >= HPB. With MG_N_HG_T template:
//   MG_N_HG_T=1 (HEADS_PER_CTA=16) → NUM_HEADS=16 dual-cache (replaces SG@16
//                                    for swa+dual_cache layers where SG is
//                                    single-cache only)
//   MG_N_HG_T=2 (HEADS_PER_CTA=32) → NUM_HEADS in {32,64,128} (legacy path)
// Single-cache NUM_HEADS=16 still uses the SG kernel above (no change).
// ============================================================================

// Default MG_N_HG used by the SmemLayoutMG / SmemPtrsMG types (which are
// shared across MG_N_HG_T=1 and MG_N_HG_T=2 — see the comment in the kernel
// body). The non-default instantiation just wastes a bit of unused smem.
static constexpr int MG_N_HG_DEFAULT = 2;
static constexpr int MG_HEADS_PER_CTA_DEFAULT = MG_N_HG_DEFAULT * HPB;  // 32

// Dual-cache MG prefill (Design A: phase-within-block, online softmax persists
// across phases). When TOPK_EXTRA == 0 the extra-phase branches dead-code-
// elide and behavior is bit-identical to the prior single-cache kernel.
// When TOPK_EXTRA > 0 the outer loop iterates over NI = TOPK/BI tiles from
// KV_cache followed by NI_EXTRA = TOPK_EXTRA/BI tiles from KV_cache_extra,
// sharing one set of online-softmax accumulators so the softmax denominator
// is computed over the union of indices.
template <ModelType MT, ComputeMode CM, int NUM_HEADS, int TOPK, int PAGE_BLOCK_SIZE, int TOPK_EXTRA = 0, int PAGE_BLOCK_SIZE_EXTRA = PAGE_BLOCK_SIZE, int MG_N_HG_T = MG_N_HG_DEFAULT>
__global__ void __launch_bounds__(BLOCK_THREADS, 1)
sparse_mla_prefill_mg_kernel(
    const bf16* __restrict__ Q,
    const uint8_t* __restrict__ KV_cache,
    const int32_t* __restrict__ indices,
    const uint8_t* __restrict__ KV_cache_extra,   // nullptr when TOPK_EXTRA==0
    const int32_t* __restrict__ indices_extra,     // nullptr when TOPK_EXTRA==0
    bf16* __restrict__ output,
    float* __restrict__ out_lse,
    const float* __restrict__ attn_sink,           // [NUM_HEADS], nullable
    __grid_constant__ const PrefillColdParams cold)
{
    // Per-instantiation MG_N_HG / MG_HEADS_PER_CTA. With MG_N_HG_T=1 the
    // kernel processes one head group per CTA (HEADS_PER_CTA=16). The
    // SmemLayoutMG / SmemPtrsMG types are still parameterised on the default
    // N_HG=2 layout, so the MG_N_HG_T=1 instantiation wastes ~half the MG
    // smem (one unused q_nope/q_sc slot + half of m_smem/l_smem/reduce_buf/
    // w_smem) but it keeps the per-group loop `for (g = 0; g < MG_N_HG; ++g)`
    // unchanged (g=0 only) and avoids a full SmemLayoutMG retemplate.
    constexpr int MG_N_HG = MG_N_HG_T;
    constexpr int MG_HEADS_PER_CTA = MG_N_HG_T * HPB;

    const float sm_scale = cold.sm_scale;
    const int num_tokens = cold.num_tokens;
    constexpr int page_block_size = PAGE_BLOCK_SIZE;
    constexpr int page_block_size_extra = PAGE_BLOCK_SIZE_EXTRA;
    const size_t stride_kv_block = cold.stride_kv_block;
    const size_t stride_kv_block_extra = cold.stride_kv_block_extra;
    using KV = KVCacheTraits<MT>;
    using CT = ComputeTraits<MT, CM>;
    using LMG = SmemLayoutMG<MT, CM>;
    using IO = KVIOTraits<MT>;
    using SMG = SmemPtrsMG<MT, CM>;

    static constexpr int NI = TOPK / BI;
    static constexpr int NI_EXTRA = TOPK_EXTRA / BI;
    static constexpr int NI_TOTAL = NI + NI_EXTRA;
    static_assert(NUM_HEADS % MG_HEADS_PER_CTA == 0,
        "NUM_HEADS must be a multiple of MG_HEADS_PER_CTA = MG_N_HG_T * HPB");
    static constexpr int REPLICATE_H = NUM_HEADS / MG_HEADS_PER_CTA;
    static constexpr int QK_NOPE_KSTEPS = KV::QUANT_TILE / 32;

    const int s_i = blockIdx.x / REPLICATE_H;
    const int h_tile = blockIdx.x % REPLICATE_H;
    const int h_start = h_tile * MG_HEADS_PER_CTA;
    if (s_i >= num_tokens) return;

    const int topk_len = cold.topk_length ? __ldg(cold.topk_length + s_i) : TOPK;
    const int actual_ni = (topk_len + BI - 1) / BI;
    const int topk_len_extra = (TOPK_EXTRA == 0) ? 0
        : (cold.topk_length_extra ? __ldg(cold.topk_length_extra + s_i) : TOPK_EXTRA);

    const int warp_rank = threadIdx.x / 32;
    const int wy = warp_rank / 4;

    extern __shared__ char smem_raw[];
    auto sm = SMG::init(smem_raw);

    if (threadIdx.x == 0) {
        mbarrier_init(sm.mbar_kv + 0, 1);
        mbarrier_init(sm.mbar_kv + 1, 1);
    }
    bar_sync_t<3, BLOCK_THREADS>();

    // ── IO warps (identical to SG, plus dual-cache phase switch) ────
    if (wy == 2) {
        asm volatile("setmaxnreg.dec.sync.aligned.u32 %0;\n" :: "n"(32));

        const int io_tid = threadIdx.x - N_MATH_WARPS * 32;
        const int32_t* idx_base = indices + (size_t)s_i * TOPK;
        const uint64_t kv_l2_policy = create_l2_evict_first_policy();

        // Per-tile data-source selection. When TOPK_EXTRA == 0, helpers
        // collapse via if-constexpr to the existing single-cache form.
        auto tile_kv_ptr = [&] (int ti) -> const uint8_t* {
            if constexpr (TOPK_EXTRA == 0) return KV_cache;
            return (ti < NI) ? KV_cache : KV_cache_extra;
        };
        auto tile_stride = [&] (int ti) -> size_t {
            if constexpr (TOPK_EXTRA == 0) return stride_kv_block;
            return (ti < NI) ? stride_kv_block : stride_kv_block_extra;
        };
        auto tile_idx_ptr = [&] (int ti) -> const int32_t* {
            if constexpr (TOPK_EXTRA == 0) return idx_base + ti * BI;
            if (ti < NI) return idx_base + ti * BI;
            return indices_extra + (size_t)s_i * TOPK_EXTRA + (ti - NI) * BI;
        };

        // Prologue: gather tile 0 (always main cache; idx_base + 0 == ptr).
        // Scales first: io_gather_scales is synchronous (plain stores), no
        // mbar signal. The math warps wake up on mbar_kv (signaled by the
        // bulk gather's cp.async.bulk completion). If scales were gathered
        // AFTER the bulk gather, the bulk could complete while scales are
        // still in flight, and math would read partial / stale scales.
        // Ordering scales-then-bulk + threadfence_block ensures scales are
        // visible before bulk-completion (and thus before math wake-up).
        // The MG_N_HG_T=1 dispatch (NUM_HEADS=16) has half the math work per
        // iter, which narrows the natural race window and exposes the bug —
        // caught by compute-sanitizer racecheck (write @ kv_cache_io.cuh:99
        // vs read @ prefill_kernel.cuh:775).
        io_gather_scales<MT, PAGE_BLOCK_SIZE>(
            sm.kv_scale_bufs[0], idx_base, KV_cache, io_tid, stride_kv_block);
        __threadfence_block();
        io_bulk_gather_tile<MT, PAGE_BLOCK_SIZE, true>(
            sm.kv_bufs[0], idx_base, KV_cache, sm.mbar_kv + 0, io_tid,
            stride_kv_block, kv_l2_policy);

        // For dual-cache (TOPK_EXTRA > 0) we always iterate NI_TOTAL — topk_length
        // masking happens at the QK stage. For single-cache, short-circuit at
        // actual_ni (saves IO bandwidth when topk_length < TOPK).
        const int loop_bound = (TOPK_EXTRA == 0) ? actual_ni : NI_TOTAL;
        #pragma unroll 1
        for (int ti = 0; ti < loop_bound; ti++) {
            if (ti + 1 < loop_bound) {
                const uint8_t* next_kv = tile_kv_ptr(ti + 1);
                const size_t   next_stride = tile_stride(ti + 1);
                const int32_t* next_idx = tile_idx_ptr(ti + 1);
                // Scales-first ordering (see prologue comment): scales sync
                // before bulk gather signals mbar so math sees them on wake.
                if constexpr (TOPK_EXTRA == 0) {
                    io_gather_scales<MT, PAGE_BLOCK_SIZE>(
                        sm.kv_scale_bufs[(ti + 1) & 1],
                        next_idx, next_kv, io_tid,
                        next_stride);
                    __threadfence_block();
                    io_bulk_gather_tile<MT, PAGE_BLOCK_SIZE, true>(
                        sm.kv_bufs[(ti + 1) & 1],
                        next_idx, next_kv,
                        sm.mbar_kv + ((ti + 1) & 1), io_tid,
                        next_stride, kv_l2_policy);
                } else {
                    if (ti + 1 < NI) {
                        io_gather_scales<MT, PAGE_BLOCK_SIZE>(
                            sm.kv_scale_bufs[(ti + 1) & 1],
                            next_idx, next_kv, io_tid,
                            next_stride);
                        __threadfence_block();
                        io_bulk_gather_tile<MT, PAGE_BLOCK_SIZE, true>(
                            sm.kv_bufs[(ti + 1) & 1],
                            next_idx, next_kv,
                            sm.mbar_kv + ((ti + 1) & 1), io_tid,
                            next_stride, kv_l2_policy);
                    } else {
                        io_gather_scales<MT, PAGE_BLOCK_SIZE_EXTRA>(
                            sm.kv_scale_bufs[(ti + 1) & 1],
                            next_idx, next_kv, io_tid,
                            next_stride);
                        __threadfence_block();
                        io_bulk_gather_tile<MT, PAGE_BLOCK_SIZE_EXTRA, true>(
                            sm.kv_bufs[(ti + 1) & 1],
                            next_idx, next_kv,
                            sm.mbar_kv + ((ti + 1) & 1), io_tid,
                            next_stride, kv_l2_policy);
                    }
                }
            }
            bar_sync_t<1, BLOCK_THREADS>();
        }

    // ── Math warps ──────────────────────────────────────────────────
    } else {
        asm volatile("setmaxnreg.inc.sync.aligned.u32 %0;\n" :: "n"(232));

        const int lane = threadIdx.x & 31;
        const int mwarp = warp_rank;
        const int gid = lane >> 2, tid = lane & 3;
        const float sm_scale_log2e = sm_scale * LOG2E;
        const int32_t* idx_base = indices + (size_t)s_i * TOPK;

        // Per-tile data-source selection (mirror of the IO-warp helpers).
        // Dead-code-elided to single-cache form when TOPK_EXTRA == 0.
        auto tile_kv_ptr = [&] (int ti) -> const uint8_t* {
            if constexpr (TOPK_EXTRA == 0) return KV_cache;
            return (ti < NI) ? KV_cache : KV_cache_extra;
        };
        auto tile_stride = [&] (int ti) -> size_t {
            if constexpr (TOPK_EXTRA == 0) return stride_kv_block;
            return (ti < NI) ? stride_kv_block : stride_kv_block_extra;
        };
        auto tile_idx_ptr = [&] (int ti) -> const int32_t* {
            if constexpr (TOPK_EXTRA == 0) return idx_base + ti * BI;
            if (ti < NI) return idx_base + ti * BI;
            return indices_extra + (size_t)s_i * TOPK_EXTRA + (ti - NI) * BI;
        };

        // ── Quantize Q for both groups (serial, reuse reduce_buf) ───
        #pragma unroll
        for (int g = 0; g < MG_N_HG; g++) {
            const bf16* q_base_g = Q + (size_t)s_i * NUM_HEADS * KV::D_QK
                                     + (size_t)(h_start + g * HPB) * KV::D_QK;
            quantize_q_to_smem<MT, MATH_THREADS>(
                sm.q_nope_fp8[g], sm.q_nope_sc[g],
                sm.q_rope + g * HPB * D_ROPE,
                q_base_g, sm.reduce_buf);
        }

        // Preload Q rope to registers for both groups
        QRopeRegs q_rope_regs[MG_N_HG];
        #pragma unroll
        for (int g = 0; g < MG_N_HG; g++)
            q_rope_regs[g] = preload_q_rope_regs(sm.q_rope + g * HPB * D_ROPE, lane);

        for (int i = threadIdx.x; i < MG_N_HG * HPB; i += MATH_THREADS)
            sm.m_smem[i] = -1e30f;

        // Per-group accumulators
        float acc_o[MG_N_HG][CT::ACC_TILES][4];
        #pragma unroll
        for (int g = 0; g < MG_N_HG; g++)
            #pragma unroll
            for (int t = 0; t < CT::ACC_TILES; t++)
                acc_o[g][t][0] = acc_o[g][t][1] = acc_o[g][t][2] = acc_o[g][t][3] = 0.f;

        float acc_rope[MG_N_HG][4];
        #pragma unroll
        for (int g = 0; g < MG_N_HG; g++)
            acc_rope[g][0] = acc_rope[g][1] = acc_rope[g][2] = acc_rope[g][3] = 0.f;

        // Deferred row_sum accumulators (register-only, no smem per tile)
        float warp_l_partial[MG_N_HG][2] = {};

        bar_sync_t<2, MATH_THREADS>();
        mbarrier_wait_parity(sm.mbar_kv + 0, 0);

        // ── Main loop ───────────────────────────────────────────────
        // Same loop_bound rule as IO: dual-cache iterates NI_TOTAL (mask via QK),
        // single-cache short-circuits at actual_ni.
        const int math_loop_bound = (TOPK_EXTRA == 0) ? actual_ni : NI_TOTAL;
        #pragma unroll 1
        for (int ti = 0; ti < math_loop_bound; ti++) {
            uint8_t* kv_smem = sm.kv_bufs[ti & 1];
            const int32_t* ib = tile_idx_ptr(ti);
            // Per-tile global KV pointer / stride. When TOPK_EXTRA == 0 these
            // collapse to the original constants.
            const uint8_t* kv_global = tile_kv_ptr(ti);
            const size_t stride_kv_block_now = tile_stride(ti);
            const int page_block_size_now =
                (TOPK_EXTRA == 0 || ti < NI)
                ? page_block_size : page_block_size_extra;
            const int qk_nb = mwarp * ENTRIES_PER_WARP;
            uint8_t* kv_warp_base = kv_smem + qk_nb * KV::KV_SMEM_STRIDE;

            // Entry base: only gid's entry needed (rope prefetch + QK rope)
            const uint8_t* entry_base_gid;
            {
                int idx = ib[qk_nb + gid];
                idx = (idx >= 0) ? idx : 0;
                if constexpr (KV::V_HAS_ROPE) {
                    int bi_e = idx / page_block_size_now;
                    int li_e = idx % page_block_size_now;
                    entry_base_gid = kv_global + (size_t)bi_e * stride_kv_block_now
                                               + (size_t)li_e * IO::IO_STRIDE;
                } else {
                    entry_base_gid = kv_global + (size_t)idx * IO::IO_STRIDE;
                }
            }

            KVRopePrefetch rope_pf = prefetch_kv_rope(
                reinterpret_cast<const bf16*>(entry_base_gid + KV::KV_ROPE_GMEM_OFFSET), lane);

            // Init per-group w_head_sc_all
            for (int i = threadIdx.x; i < MG_N_HG * CT::N_V_CHUNKS * HPB; i += MATH_THREADS)
                sm.w_head_sc_all[i] = 0.f;

            // ── QK + softmax for both groups ────────────────────────
            float w_grp[MG_N_HG][4];
            float vsc_cache[MG_N_HG][CT::N_V_CHUNKS][2];

            #pragma unroll
            for (int g = 0; g < MG_N_HG; g++) {
                const uint8_t* kv_gid_base = kv_warp_base + gid * KV::KV_SMEM_STRIDE;

                // QK nope (block-scaled FP8 MMA)
                float qk[4] = {0.f, 0.f, 0.f, 0.f};
                #pragma unroll
                for (int blk = 0; blk < KV::NUM_SCALES; blk++) {
                    uint8_t sfa = fp32_to_ue8m0(
                        sm.q_nope_sc[g][(gid + (lane & 1) * 8) * KV::NUM_SCALES + blk]);
                    uint8_t sfb;
                    if constexpr (KV::SCALE_IN_KV_SMEM) {
                        sfb = fp32_to_ue8m0(
                            reinterpret_cast<const float*>(kv_gid_base + KV::D_NOPE)[blk]);
                    } else {
                        sfb = sm.kv_scale_bufs[ti & 1][(qk_nb + gid) * KV::SCALE_BYTES_PER_TOKEN + blk];
                    }
                    #pragma unroll
                    for (int ks = 0; ks < QK_NOPE_KSTEPS; ks++) {
                        int ko = blk * KV::QUANT_TILE + ks * 32;
                        uint32_t a0, a1, a2, a3, b0, b1;
                        ldmatrix_load_A_fp8(a0, a1, a2, a3,
                            sm.q_nope_fp8[g] + ko, KV::Q_NOPE_STRIDE, lane);
                        ldmatrix_load_B_fp8(b0, b1,
                            kv_warp_base + ko, KV::KV_SMEM_STRIDE, lane);
                        MmaFp8Result r = mma_fp8_block_scaled_m16n8k32(
                            a0, a1, a2, a3, b0, b1,
                            qk[0], qk[1], qk[2], qk[3], sfa, sfb);
                        qk[0] = r.d0; qk[1] = r.d1; qk[2] = r.d2; qk[3] = r.d3;
                    }
                }

                // QK rope (reuses prefetched B operands)
                compute_qk_rope(qk, q_rope_regs[g], rope_pf);

                // Invalid index masking + topk_length overflow. For dual-cache
                // (TOPK_EXTRA > 0): main-phase tiles (ti < NI) use topk_len
                // against absolute index (ti * BI + e); extra-phase tiles
                // (ti >= NI) use topk_len_extra against the relative index
                // ((ti - NI) * BI + e).
                {
                    int e0 = qk_nb + tid * 2, e1 = e0 + 1;
                    if (ib[e0] < 0) { qk[0] = -1e30f; qk[2] = -1e30f; }
                    if (ib[e1] < 0) { qk[1] = -1e30f; qk[3] = -1e30f; }
                    if constexpr (TOPK_EXTRA == 0) {
                        if (cold.topk_length != nullptr) {
                            int a0 = ti * BI + e0, a1 = ti * BI + e1;
                            if (a0 >= topk_len) { qk[0] = -1e30f; qk[2] = -1e30f; }
                            if (a1 >= topk_len) { qk[1] = -1e30f; qk[3] = -1e30f; }
                        }
                    } else {
                        if (ti < NI) {
                            if (cold.topk_length != nullptr) {
                                int a0 = ti * BI + e0, a1 = ti * BI + e1;
                                if (a0 >= topk_len) { qk[0] = -1e30f; qk[2] = -1e30f; }
                                if (a1 >= topk_len) { qk[1] = -1e30f; qk[3] = -1e30f; }
                            }
                        } else {
                            if (cold.topk_length_extra != nullptr) {
                                int a0 = (ti - NI) * BI + e0, a1 = (ti - NI) * BI + e1;
                                if (a0 >= topk_len_extra) { qk[0] = -1e30f; qk[2] = -1e30f; }
                                if (a1 >= topk_len_extra) { qk[1] = -1e30f; qk[3] = -1e30f; }
                            }
                        }
                    }
                }

                float s[4] = { qk[0] * sm_scale_log2e, qk[1] * sm_scale_log2e,
                               qk[2] * sm_scale_log2e, qk[3] * sm_scale_log2e };

                // Warp max → reduce_buf (per-group offset)
                float lm0, lm1;
                softmax_warp_max(s, lm0, lm1);
                if (tid == 0) {
                    sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + mwarp * HPB + gid] = lm0;
                    sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + mwarp * HPB + gid + 8] = lm1;
                }
                // Store s for later use
                w_grp[g][0] = s[0]; w_grp[g][1] = s[1];
                w_grp[g][2] = s[2]; w_grp[g][3] = s[3];
            }
            bar_sync_t<2, MATH_THREADS>();

            // Cross-warp max for both groups
            if (threadIdx.x < MG_N_HG * HPB) {
                int g = threadIdx.x / HPB, h = threadIdx.x % HPB;
                float old_m = sm.m_smem[g * SMG::ML_GRP_STRIDE + h], tm = -1e30f;
                #pragma unroll
                for (int w = 0; w < N_MATH_WARPS; w++)
                    tm = fmaxf(tm, sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + w * HPB + h]);
                float nm = fmaxf(old_m, tm);
                float alpha = exp2f(old_m - nm);
                sm.m_smem[g * SMG::ML_GRP_STRIDE + h] = nm;
                sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + h] = alpha;
                sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + HPB + h] = nm;
            }
            bar_sync_t<2, MATH_THREADS>();

            // Rescale + exp weights for both groups
            #pragma unroll
            for (int g = 0; g < MG_N_HG; g++) {
                float alpha0 = sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + gid];
                float alpha1 = sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + gid + 8];
                float nm0 = sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + HPB + gid];
                float nm1 = sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + HPB + gid + 8];

                if (alpha0 < 1.0f || alpha1 < 1.0f) {
                    #pragma unroll
                    for (int t = 0; t < CT::ACC_TILES; t++) {
                        acc_o[g][t][0] *= alpha0; acc_o[g][t][1] *= alpha0;
                        acc_o[g][t][2] *= alpha1; acc_o[g][t][3] *= alpha1;
                    }
                    if constexpr (KV::V_HAS_ROPE) {
                        acc_rope[g][0] *= alpha0; acc_rope[g][1] *= alpha0;
                        acc_rope[g][2] *= alpha1; acc_rope[g][3] *= alpha1;
                    }
                    warp_l_partial[g][0] *= alpha0;
                    warp_l_partial[g][1] *= alpha1;
                }

                float w0 = exp2f(w_grp[g][0] - nm0), w1 = exp2f(w_grp[g][1] - nm0);
                float w2 = exp2f(w_grp[g][2] - nm1), w3 = exp2f(w_grp[g][3] - nm1);
                w_grp[g][0] = w0; w_grp[g][1] = w1;
                w_grp[g][2] = w2; w_grp[g][3] = w3;

                float ls0, ls1;
                softmax_warp_sum(w0, w1, w2, w3, ls0, ls1);
                warp_l_partial[g][0] += ls0;
                warp_l_partial[g][1] += ls1;

                // V scale cache + atomicMax
                const int e0i = qk_nb + tid * 2, e1i = e0i + 1;
                const uint8_t* e0_base = kv_warp_base + tid * 2 * KV::KV_SMEM_STRIDE;
                const uint8_t* e1_base = e0_base + KV::KV_SMEM_STRIDE;
                #pragma unroll
                for (int vc = 0; vc < CT::N_V_CHUNKS; vc++) {
                    if constexpr (KV::SCALE_IN_KV_SMEM) {
                        vsc_cache[g][vc][0] = reinterpret_cast<const float*>(e0_base + KV::D_NOPE)[vc];
                        vsc_cache[g][vc][1] = reinterpret_cast<const float*>(e1_base + KV::D_NOPE)[vc];
                    } else {
                        vsc_cache[g][vc][0] = ue8m0_to_fp32(sm.kv_scale_bufs[ti & 1][e0i * KV::SCALE_BYTES_PER_TOKEN + vc]);
                        vsc_cache[g][vc][1] = ue8m0_to_fp32(sm.kv_scale_bufs[ti & 1][e1i * KV::SCALE_BYTES_PER_TOKEN + vc]);
                    }
                    float ws00 = w0 * vsc_cache[g][vc][0], ws01 = w1 * vsc_cache[g][vc][1];
                    float ws10 = w2 * vsc_cache[g][vc][0], ws11 = w3 * vsc_cache[g][vc][1];
                    atomicMax(reinterpret_cast<int*>(&sm.w_head_sc_all[g * SMG::WSC_GRP_STRIDE + vc * HPB + gid]),
                        __float_as_int(fmaxf(fabsf(ws00), fabsf(ws01))));
                    atomicMax(reinterpret_cast<int*>(&sm.w_head_sc_all[g * SMG::WSC_GRP_STRIDE + vc * HPB + gid + 8]),
                        __float_as_int(fmaxf(fabsf(ws10), fabsf(ws11))));
                }
            }
            bar_sync_t<2, MATH_THREADS>();

            // Normalize w_head_sc_all (both groups)
            for (int i = threadIdx.x; i < MG_N_HG * CT::N_V_CHUNKS * HPB; i += MATH_THREADS)
                sm.w_head_sc_all[i] = fmaxf(sm.w_head_sc_all[i], 1e-10f) / FP8_MAX;
            bar_sync_t<2, MATH_THREADS>();

            // ── XV nope MMA (per-vc barrier, D2 direct B) ────────────
            {
                const int e0i = qk_nb + tid * 2, e1i = e0i + 1;

                #pragma unroll
                for (int vc = 0; vc < CT::N_V_CHUNKS; vc++) {
                    #pragma unroll
                    for (int g = 0; g < MG_N_HG; g++) {
                        float* vc_sc = sm.w_head_sc_all + g * SMG::WSC_GRP_STRIDE + vc * HPB;
                        uint8_t* cur_wfp8 = sm.w_fp8 + g * SMG::WFP8_GRP_SIZE;
                        float si0 = 1.f / vc_sc[gid], si1 = 1.f / vc_sc[gid + 8];
                        float w0 = w_grp[g][0], w1 = w_grp[g][1];
                        float w2 = w_grp[g][2], w3 = w_grp[g][3];
                        float vsc0 = vsc_cache[g][vc][0], vsc1 = vsc_cache[g][vc][1];
                        float ws00 = w0 * vsc0, ws01 = w1 * vsc1;
                        float ws10 = w2 * vsc0, ws11 = w3 * vsc1;
                        __nv_fp8_e4m3 f00(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws00 * si0)));
                        __nv_fp8_e4m3 f01(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws01 * si0)));
                        __nv_fp8_e4m3 f10(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws10 * si1)));
                        __nv_fp8_e4m3 f11(fmaxf(-FP8_MAX, fminf(FP8_MAX, ws11 * si1)));
                        cur_wfp8[gid * (BI + 16) + e0i] = f00.__x;
                        cur_wfp8[gid * (BI + 16) + e1i] = f01.__x;
                        cur_wfp8[(gid + 8) * (BI + 16) + e0i] = f10.__x;
                        cur_wfp8[(gid + 8) * (BI + 16) + e1i] = f11.__x;
                    }

                    bar_sync_t<2, MATH_THREADS>();

                    #pragma unroll
                    for (int g = 0; g < MG_N_HG; g++) {
                        float* vc_sc = sm.w_head_sc_all + g * SMG::WSC_GRP_STRIDE + vc * HPB;
                        uint8_t* cur_wfp8 = sm.w_fp8 + g * SMG::WFP8_GRP_SIZE;
                        #pragma unroll
                        for (int nt = 0; nt < CT::NT_PER_WARP_XV; nt++) {
                            int ti_acc = vc * CT::NT_PER_WARP_XV + nt;
                            int dim = vc * CT::V_CHUNK + mwarp * (CT::NT_PER_WARP_XV * 8) + nt * 8;
                            float xv[4] = {0.f, 0.f, 0.f, 0.f};
                            #pragma unroll
                            for (int kstep = 0; kstep < CT::XV_KSTEPS; kstep++) {
                                int ko = kstep * 32;
                                uint32_t a0, a1, a2, a3, b0, b1;
                                ldmatrix_load_A_fp8(a0, a1, a2, a3,
                                    cur_wfp8 + ko, BI + 16, lane);
                                d2_load_b_fp8<KV::KV_SMEM_STRIDE>(b0, b1,
                                    kv_smem, kstep * 32, dim, lane);
                                MmaFp8Result r = mma_fp8_m16n8k32(
                                    a0, a1, a2, a3, b0, b1,
                                    xv[0], xv[1], xv[2], xv[3]);
                                xv[0] = r.d0; xv[1] = r.d1; xv[2] = r.d2; xv[3] = r.d3;
                            }
                            float sc0 = vc_sc[gid], sc1 = vc_sc[gid + 8];
                            acc_o[g][ti_acc][0] += xv[0] * sc0; acc_o[g][ti_acc][1] += xv[1] * sc0;
                            acc_o[g][ti_acc][2] += xv[2] * sc1; acc_o[g][ti_acc][3] += xv[3] * sc1;
                        }
                    }
                    // Barrier guards vc=k's ldmatrix reads against vc=k+1's
                    // FP8-weight writes to the SAME w_fp8 region. With
                    // MG_N_HG_T=1 the per-vc read window is half the work
                    // (one group instead of two), narrowing the natural
                    // timing gap and exposing the race that the bar at the
                    // end of the loop alone doesn't cover. Caught by
                    // compute-sanitizer racecheck against ldmatrix.x4.
                    bar_sync_t<2, MATH_THREADS>();
                }
            }

            // ── XV rope BF16 MMA (MODEL1, both groups) ──────────────
            if constexpr (KV::V_HAS_ROPE) {
                bar_sync_t<2, MATH_THREADS>();
                if constexpr (TOPK_EXTRA == 0) {
                    #pragma unroll
                    for (int g = 0; g < MG_N_HG; g++) {
                        xv_rope_mma<MT, PAGE_BLOCK_SIZE>(
                            acc_rope[g], w_grp[g][0], w_grp[g][1], w_grp[g][2], w_grp[g][3],
                            ib, kv_global, mwarp, lane, stride_kv_block_now,
                            reinterpret_cast<bf16*>(sm.w_fp8));
                    }
                } else {
                    if (ti < NI) {
                        #pragma unroll
                        for (int g = 0; g < MG_N_HG; g++) {
                            xv_rope_mma<MT, PAGE_BLOCK_SIZE>(
                                acc_rope[g], w_grp[g][0], w_grp[g][1], w_grp[g][2], w_grp[g][3],
                                ib, kv_global, mwarp, lane, stride_kv_block_now,
                                reinterpret_cast<bf16*>(sm.w_fp8));
                        }
                    } else {
                        #pragma unroll
                        for (int g = 0; g < MG_N_HG; g++) {
                            xv_rope_mma<MT, PAGE_BLOCK_SIZE_EXTRA>(
                                acc_rope[g], w_grp[g][0], w_grp[g][1], w_grp[g][2], w_grp[g][3],
                                ib, kv_global, mwarp, lane, stride_kv_block_now,
                                reinterpret_cast<bf16*>(sm.w_fp8));
                        }
                    }
                }
            }

            bar_arrive_t<1, BLOCK_THREADS>();
            if (ti + 1 < math_loop_bound) {
                const int next_phase = ((ti + 1) >> 1) & 1;
                mbarrier_wait_parity(sm.mbar_kv + ((ti + 1) & 1), next_phase);
            }
        }

        // ── Finalize deferred row_sum ───────────────────────────────
        // Write warp_l_partial to smem for cross-warp reduction
        #pragma unroll
        for (int g = 0; g < MG_N_HG; g++) {
            if (tid == 0) {
                sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + mwarp * HPB + gid] = warp_l_partial[g][0];
                sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + mwarp * HPB + gid + 8] = warp_l_partial[g][1];
            }
        }
        bar_sync_t<2, MATH_THREADS>();

        if (threadIdx.x < MG_N_HG * HPB) {
            int g = threadIdx.x / HPB, h = threadIdx.x % HPB;
            float ts = 0.f;
            #pragma unroll
            for (int w = 0; w < N_MATH_WARPS; w++)
                ts += sm.reduce_buf[g * SMG::REDUCE_GRP_STRIDE + w * HPB + h];
            sm.l_smem[g * SMG::ML_GRP_STRIDE + h] = ts;
        }
        bar_sync_t<2, MATH_THREADS>();

        // ── Epilogue: BF16 output for both groups (serial) ─────────
        // Reuse kv_bufs[0] for BF16 staging (16KB needed, 29-33KB available)
        bf16* staging_bf16 = reinterpret_cast<bf16*>(sm.kv_bufs[0]);
        constexpr int BF16_STAGING_STRIDE = D_V;
        constexpr size_t h_stride = D_V;
        constexpr size_t token_stride = (size_t)NUM_HEADS * D_V;

        #pragma unroll
        for (int g = 0; g < MG_N_HG; g++) {
            // attn_sink folded into the normalizer (FlashMLA V4 convention).
            // See SG epilogue for full derivation.
            float il0, il1;
            if (cold.attn_sink != nullptr) {
                int h0 = h_start + g * HPB + gid, h1 = h0 + 8;
                float s0 = __ldg(cold.attn_sink + h0) * LOG2E;
                float s1 = __ldg(cold.attn_sink + h1) * LOG2E;
                float d0 = sm.l_smem[g * SMG::ML_GRP_STRIDE + gid]
                          + exp2f(s0 - sm.m_smem[g * SMG::ML_GRP_STRIDE + gid]);
                float d1 = sm.l_smem[g * SMG::ML_GRP_STRIDE + gid + 8]
                          + exp2f(s1 - sm.m_smem[g * SMG::ML_GRP_STRIDE + gid + 8]);
                il0 = (d0 > 0.f) ? (1.f / d0) : 0.f;
                il1 = (d1 > 0.f) ? (1.f / d1) : 0.f;
            } else {
                il0 = (sm.l_smem[g * SMG::ML_GRP_STRIDE + gid] > 0.f)
                    ? (1.f / sm.l_smem[g * SMG::ML_GRP_STRIDE + gid]) : 0.f;
                il1 = (sm.l_smem[g * SMG::ML_GRP_STRIDE + gid + 8] > 0.f)
                    ? (1.f / sm.l_smem[g * SMG::ML_GRP_STRIDE + gid + 8]) : 0.f;
            }

            #pragma unroll
            for (int t = 0; t < CT::ACC_TILES; t++) {
                constexpr int _NT8 = CT::NT_PER_WARP_XV * 8;
                int c = t / CT::NT_PER_WARP_XV, lnt = t % CT::NT_PER_WARP_XV;
                int d0 = c * CT::V_CHUNK + mwarp * _NT8 + lnt * 8 + tid * 2;
                staging_bf16[gid * BF16_STAGING_STRIDE + d0]       = __float2bfloat16(acc_o[g][t][0] * il0);
                staging_bf16[gid * BF16_STAGING_STRIDE + d0 + 1]   = __float2bfloat16(acc_o[g][t][1] * il0);
                staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0]   = __float2bfloat16(acc_o[g][t][2] * il1);
                staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0+1] = __float2bfloat16(acc_o[g][t][3] * il1);
            }

            if constexpr (KV::V_HAS_ROPE) {
                int n_start = mwarp * 8;
                int d0 = KV::D_NOPE + n_start + tid * 2;
                staging_bf16[gid * BF16_STAGING_STRIDE + d0]       = __float2bfloat16(acc_rope[g][0] * il0);
                staging_bf16[gid * BF16_STAGING_STRIDE + d0 + 1]   = __float2bfloat16(acc_rope[g][1] * il0);
                staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0]   = __float2bfloat16(acc_rope[g][2] * il1);
                staging_bf16[(gid+8) * BF16_STAGING_STRIDE + d0+1] = __float2bfloat16(acc_rope[g][3] * il1);
            }
            bar_sync_t<2, MATH_THREADS>();

            // Coalesced write
            {
                const int g_h_start = h_start + g * HPB;
                const size_t out_base = (size_t)s_i * token_stride + (size_t)g_h_start * h_stride;
                constexpr int BF16_PER_STORE = 8;
                constexpr int STORES_PER_HEAD = D_V / BF16_PER_STORE;
                for (int idx = threadIdx.x; idx < HPB * STORES_PER_HEAD; idx += MATH_THREADS) {
                    int h = idx / STORES_PER_HEAD;
                    int d8 = (idx - h * STORES_PER_HEAD) * BF16_PER_STORE;
                    uint4 v = *reinterpret_cast<const uint4*>(
                        &staging_bf16[h * BF16_STAGING_STRIDE + d8]);
                    *reinterpret_cast<uint4*>(&output[out_base + h * h_stride + d8]) = v;
                }
            }

            // Write LSE for this group (merged with attn_sink if present)
            if (threadIdx.x < HPB) {
                int h = threadIdx.x;
                float lse = softmax_lse(sm.m_smem[g * SMG::ML_GRP_STRIDE + h],
                                         sm.l_smem[g * SMG::ML_GRP_STRIDE + h]);
                if (cold.attn_sink != nullptr) {
                    float sink_log2 = __ldg(cold.attn_sink + h_start + g * HPB + h) * LOG2E;
                    if (lse != -1e30f)
                        lse += log2f(1.f + exp2f(sink_log2 - lse));
                    else
                        lse = sink_log2;
                }
                size_t lse_idx = (size_t)s_i * NUM_HEADS + (h_start + g * HPB + h);
                out_lse[lse_idx] = lse;
            }

            if (g < MG_N_HG - 1)
                bar_sync_t<2, MATH_THREADS>();
        }
    }
}
